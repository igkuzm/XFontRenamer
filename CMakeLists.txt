cmake_minimum_required(VERSION 3.0)

set(TARGET XFontRenamer)

project(${TARGET} 
	VERSION 1.0 
	DESCRIPTION "GUI application for font renaming"
	HOMEPAGE_URL ""
	LANGUAGES C OBJC 
)

set (CMAKE_C_STANDARD 11)

LIST(APPEND RESOURCE_FILES
	Dropzone.png
)

file(GLOB TARGET_SOURCES "*.m")
file(GLOB SANDBOX_FILEACCESS "AppSandboxFileAccess/*.m")
add_executable(${TARGET} 
	${TARGET_SOURCES}
	${SANDBOX_FILEACCESS}
	${RESOURCE_FILES}
)

target_link_libraries(${TARGET}
	"-framework Cocoa"
)

#add icons
target_sources(${TARGET} PRIVATE AppIcons/Assets.xcassets)
set_source_files_properties(AppIcons/Assets.xcassets PROPERTIES MACOSX_PACKAGE_LOCATION Resources)
set_target_properties(${TARGET} PROPERTIES XCODE_ATTRIBUTE_ASSETCATALOG_COMPILER_APPICON_NAME "AppIcon")

#add entitiments
set_target_properties(${TARGET} PROPERTIES XCODE_ATTRIBUTE_CODE_SIGN_ENTITLEMENTS "${CMAKE_SOURCE_DIR}/${TARGET}.entitlements")

set_target_properties(${TARGET} PROPERTIES
	
	#MacOS Bundle
	MACOSX_BUNDLE TRUE
	MACOSX_BUNDLE_INFO_PLIST ${CMAKE_CURRENT_SOURCE_DIR}/Info.plist
	MACOSX_BUNDLE_BUNDLE_NAME ${TARGET}
	MACOSX_BUNDLE_GUI_IDENTIFIER kuzm.ig.${TARGET}
	MACOSX_BUNDLE_BUNDLE_VERSION ${CMAKE_PROJECT_VERSION}
	RESOURCE "${RESOURCE_FILES}"

	#code singing
	XCODE_ATTRIBUTE_DEVELOPMENT_TEAM "B77VTA4T4A"
	XCODE_ATTRIBUTE_CODE_SIGN_IDENTITY "Mac Developer"
	XCODE_ATTRIBUTE_PRODUCT_BUNDLE_IDENTIFIER kuzm.ig.${TARGET}	

	XCODE_ATTRIBUTE_ARCHS "arm64 x86_64"
	XCODE_ATTRIBUTE_MACOSX_DEPLOYMENT_TARGET "10.13"

	#bug fix for XCode archiving
	XCODE_ATTRIBUTE_INSTALL_PATH "$(LOCAL_APPS_DIR)"
	XCODE_ATTRIBUTE_SKIP_INSTALL "NO"
	XCODE_ATTRIBUTE_ENABLE_BITCODE "NO"	
	ARCHIVE_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}	
	
)
